/****引入gradle插件*****************/
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
/****jdk版本*****************/
sourceCompatibility = 1.8

buildscript {
	repositories {
	  mavenLocal()
    	maven {    	
        	url "http://svn.etena.cn:8081/content/groups/public/"
    	}
	}	
	dependencies {
	  // web容器，支持jetty和tomcat
	  classpath 'org.akhikhl.gretty:gretty:1.2.4'
	}
}




/****maven仓库*****************/
repositories {
	mavenLocal();
    maven {    	
        url "http://svn.etena.cn:8081/content/groups/public/"
    }
    maven {    	
        url "http://repository.ow2.org/nexus/content/repositories/public/"
    }
}
/****编译编码，解决Gradle编译时出现： 编码GBK的不可映射字符*****************/
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

/****源代码文件夹设置*****************/
sourceSets {
	main {
		java {
            srcDirs = ['src/main/java','src/main/test']
        }
    	println '****************'+'src/resources'+'****************'
    	resources {
            srcDir 'resources'
        }
    }
}
project.ext.set("springVersion", "4.2.4.RELEASE")
/****依赖关系*****************/
dependencies {
	compile 'javax.servlet:jstl:1.2'
	compile 'org.springframework:spring-core:'+ springVersion
	compile 'org.springframework:spring-beans:'+ springVersion
	compile 'org.springframework:spring-aop:'+ springVersion
	compile 'org.springframework:spring-context:'+ springVersion
	compile 'org.springframework:spring-context-support:'+ springVersion
	compile 'org.springframework:spring-expression:'+ springVersion
	compile 'org.springframework:spring-tx:'+ springVersion
	compile 'org.springframework:spring-jdbc:'+ springVersion
	compile 'org.springframework:spring-web:'+ springVersion
  	compile 'org.springframework:spring-webmvc:'+ springVersion
  	compile 'org.dom4j:dom4j:2.0.0'
  	
	testCompile	"junit:junit:4.9"
}

/****生成jar*****************/
version = '1.0.0'//发布版本
jar {
	manifest {
        attributes 'Created-By':'zuo lang',//声明该文件的生成者
        'Implementation-Title': 'spring zuo', 
        'Implementation-Version': version
    }
	//jar包包含测试代码，用于其它项目测试调用
	from(sourceSets.test.allJava)
}
/****生成源码jar*****************/
task sourceJar(type: Jar) { 
    from sourceSets.main.allJava//主体源代码
    from sourceSets.test.allJava//测试源代码
    classifier "sources"
}
/***生成javadoc,命令行参数配置**/
javadoc {
	source = [sourceSets.main.allJava,sourceSets.test.allJava] //需要生成javadoc的源码包
	classpath=configurations.testCompile
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    options.links = ["http://docs.oracle.com/javase/8/docs/api/"]
    options.memberLevel="PACKAGE"
}
//生成javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
    classifier = 'javadoc'	    
}

task copyContext(type: Copy) {
	description '获取数据库配置文件'
    from 'src/resources/context.xml'
    into 'src/main/webapp/META-INF'
}
/****引入gretty插件*****************/	
apply plugin: 'org.akhikhl.gretty'	
gretty {
    port = 8080
    servletContainer = 'tomcat8'
	contextPath ="/"
	enableNaming=true //启用JNDI
	//scanInterval 0//关闭热部署
	/**
	 * Tomcat启动，增加启动参数，用来判断是否需要启用业务定时接口
	 * 不包含：中间件运行坏境监控-RunTimeTraceTimer
	 * 主要用于集群环境，多个Tomcat时，指定某个Tomcat运行定时接口，其他不运行
	 */
	jvmArgs  '-DuseTimer=true'
}
